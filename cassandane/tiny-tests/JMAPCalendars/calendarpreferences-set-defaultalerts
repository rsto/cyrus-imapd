#!perl
use Cassandane::Tiny;

sub test_calendarpreferences_set_defaultalerts
    :min_version_3_7 :needs_component_jmap
{
    my ($self) = @_;

    my $jmap = $self->{jmap};
    my $caldav = $self->{caldav};

    xlog "Get (non-existent) default alarms in CalendarPreferences";
    my $res = $jmap->CallMethods([
        ['CalendarPreferences/get', {
        }, 'R1'],
    ]);
    $self->assert_null($res->[0][1]{list}[0]{defaultAlertsWithTime});
    $self->assert_null($res->[0][1]{list}[0]{defaultAlertsWithoutTime});

    xlog "PROPPATCH default alerts on calendar home";
    my $proppatchXml = <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<D:propertyupdate xmlns:D="DAV:" xmlns:C="urn:ietf:params:xml:ns:caldav">
  <D:set>
    <D:prop>
<C:default-alarm-vevent-datetime>
BEGIN:VALARM
UID:c5f1c439-d18d-4c98-89e1-211ccf752dec
TRIGGER:-PT5M
ACTION:DISPLAY
DESCRIPTION:alarmTime1
END:VALARM
</C:default-alarm-vevent-datetime>
    </D:prop>
  </D:set>
  <D:set>
    <D:prop>
<C:default-alarm-vevent-date>
BEGIN:VALARM
UID:bc0c8431-503e-427a-bb2d-6a1e2fca2d3f
TRIGGER:PT0S
ACTION:DISPLAY
DESCRIPTION:alarmDate1
END:VALARM
</C:default-alarm-vevent-date>
    </D:prop>
  </D:set>
</D:propertyupdate>
EOF
    $caldav->Request('PROPPATCH', "/dav/calendars/user/cassandane",
        $proppatchXml, 'Content-Type' => 'text/xml');

    xlog "Get default alarms in CalendarPreferences";
    $res = $jmap->CallMethods([
        ['CalendarPreferences/get', {
        }, 'R1'],
    ]);
    $self->assert_not_null($res->[0][1]{list}[0]{defaultAlertsWithTime}{
        'c5f1c439-d18d-4c98-89e1-211ccf752dec'});
    $self->assert_not_null($res->[0][1]{list}[0]{defaultAlertsWithoutTime}{
        'bc0c8431-503e-427a-bb2d-6a1e2fca2d3f'});

    xlog "Update default alarms in CalendarPreferences";
    $res = $jmap->CallMethods([
        ['CalendarPreferences/set', {
            update => {
                singleton => {
                    defaultAlertsWithTime => {
                        alert1 => {
                            '@type' => 'Alert',
                            uid => '95f5ad59-ee19-40c3-b900-04733478b5a0',
                            trigger => {
                                '@type' => 'OffsetTrigger',
                                relativeTo => 'start',
                                offset => '-PT1H',
                            },
                            action => 'email',
                        },
                    }
                },
            },
        }, 'R1'],
    ]);
    $self->assert(exists $res->[0][1]{updated}{singleton});

    xlog "PROPFIND for default alarms on calendar home";
    my $propfindXml = <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<D:propfind xmlns:D="DAV:" xmlns:C="urn:ietf:params:xml:ns:caldav">
  <D:prop>
     <C:default-alarm-vevent-datetime/>
     <C:default-alarm-vevent-date/>
  </D:prop>
</D:propfind>
EOF
    $res = $caldav->Request('PROPFIND', "/dav/calendars/user/cassandane",
        $propfindXml, 'Content-Type' => 'text/xml');

    xlog "Assert default alarm with time got replaced";
    my $ps = $res->{'{DAV:}response'}[0]{'{DAV:}propstat'}[0];
    my $valarm = $ps->{'{DAV:}prop'}{
        '{urn:ietf:params:xml:ns:caldav}default-alarm-vevent-datetime'}{content};
    $self->assert($valarm =~ '95f5ad59-ee19-40c3-b900-04733478b5a0');
    $self->assert(not $valarm =~ 'c5f1c439-d18d-4c98-89e1-211ccf752dec');

    xlog "Assert default alarm without time is intact";
    $ps = $res->{'{DAV:}response'}[0]{'{DAV:}propstat'}[0];
    $valarm = $ps->{'{DAV:}prop'}{
        '{urn:ietf:params:xml:ns:caldav}default-alarm-vevent-date'}{content};
    $self->assert($valarm =~ 'bc0c8431-503e-427a-bb2d-6a1e2fca2d3f');
    $self->assert(not $valarm =~ 'c5f1c439-d18d-4c98-89e1-211ccf752dec');

    xlog "Remove default alarms in CalendarPreferences";
    $res = $jmap->CallMethods([
        ['CalendarPreferences/set', {
            update => {
                singleton => {
                    defaultAlertsWithTime => undef,
                    defaultAlertsWithoutTime => undef,
                },
            },
        }, 'R1'],
        ['CalendarPreferences/get', {
        }, 'R2'],
    ]);
    $self->assert(exists $res->[0][1]{updated}{singleton});
    $self->assert_null($res->[1][1]{list}[0]{defaultAlertsWithTime});
    $self->assert_null($res->[1][1]{list}[0]{defaultAlertsWithoutTime});

    xlog "PROPFIND for default alarms on calendar home";
    $res = $caldav->Request('PROPFIND', "/dav/calendars/user/cassandane",
        $propfindXml, 'Content-Type' => 'text/xml');

    xlog "Assert DAV properties are gone";
    my $ps = $res->{'{DAV:}response'}[0]{'{DAV:}propstat'}[0];
    $self->assert_not_null($ps->{'{DAV:}prop'}{
        '{urn:ietf:params:xml:ns:caldav}default-alarm-vevent-datetime'});
    $self->assert_not_null($ps->{'{DAV:}prop'}{
        '{urn:ietf:params:xml:ns:caldav}default-alarm-vevent-date'});
    $self->assert_str_equals('HTTP/1.1 404 Not Found', $ps->{'{DAV:}status'}{content});
}
