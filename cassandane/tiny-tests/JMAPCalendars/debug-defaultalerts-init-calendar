#!perl
use Cassandane::Tiny;

sub test_debug_defaultalerts_init_calendar
    :min_version_3_7 :needs_component_jmap
{
    my ($self) = @_;
    my $jmap = $self->{jmap};
    my $caldav = $self->{caldav};

    my $using = [
        'urn:ietf:params:jmap:core',
        'urn:ietf:params:jmap:calendars',
        'urn:ietf:params:jmap:calendars:preferences',
        'https://cyrusimap.org/ns/jmap/calendars',
        'https://cyrusimap.org/ns/jmap/debug',
    ];

    xlog $self, "Create calendar via CalDAV";
    $caldav->Request('MKCALENDAR', "/dav/calendars/user/cassandane/test1");

    xlog $self, "Create calendar via JMAP";
    my $res = $jmap->CallMethods([
        ['Calendar/set', {
            create => {
                test2 => {
                    name => 'test2',
                },
            },
        }, 'R1'],
    ], $using);
    $self->assert_not_null($res->[0][1]{created}{test2});

    xlog $self, "Assert calendars (including Default) have zero alerts set";
    $res = $jmap->CallMethods([
        ['Calendar/get', {
            properties => [
                'debugDefaultAlertsWithTime',
                'debugDefaultAlertsWithoutTime',
                'caldavDefaultAlarmDateTime',
                'caldavDefaultAlarmDate',
            ],
        }, 'R1'],
    ], $using);
    $self->assert_num_equals(3, scalar @{$res->[0][1]{list}});

    for my $calendar (@{$res->[0][1]{list}}) {
        $self->assert_null($calendar->{caldavDefaultAlarmDateTime});
        $self->assert_null($calendar->{caldavDefaultAlarmDate});
        $self->assert_deep_equals({
                guid => '0000000000000000000000000000000000000000',
                content => '',
                isDlist => 1
        }, $calendar->{debugDefaultAlertsWithTime});
        $self->assert_deep_equals({
                guid => '0000000000000000000000000000000000000000',
                content => '',
                isDlist => 1
        }, $calendar->{debugDefaultAlertsWithoutTime});
    }
}
