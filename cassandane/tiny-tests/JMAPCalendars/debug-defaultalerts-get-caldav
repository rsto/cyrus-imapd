#!perl
use Cassandane::Tiny;

sub t
    :min_version_3_7 :needs_component_jmap
{
    my ($self, $objectType, $objectId, $davUrl) = @_;

    my $jmap = $self->{jmap};
    my $caldav = $self->{caldav};

    my $using = [
        'urn:ietf:params:jmap:core',
        'urn:ietf:params:jmap:calendars',
        'urn:ietf:params:jmap:calendars:preferences',
        'https://cyrusimap.org/ns/jmap/calendars',
        'https://cyrusimap.org/ns/jmap/debug',
    ];

    xlog $self, "Get (non-existent) CalDAV default alarms for $objectType $objectId";
    my $res = $jmap->CallMethods([
        ["$objectType/get", {
            ids => [$objectId],
            properties => [
                'caldavDefaultAlarmDateTime',
                'caldavDefaultAlarmDate',
            ],
        }, 'R1'],
    ], $using);
    $self->assert_null($res->[0][1]{list}[0]{caldavDefaultAlarmDateTime});
    $self->assert_null($res->[0][1]{list}[0]{caldavDefaultAlarmDate});

    my $valarmDateTime = <<EOF;
BEGIN:VALARM
UID:c5f1c439-d18d-4c98-89e1-211ccf752dec
TRIGGER:-PT5M
ACTION:DISPLAY
DESCRIPTION:alarmTime1
END:VALARM
EOF

    my $valarmDate = <<EOF;
BEGIN:VALARM
UID:bc0c8431-503e-427a-bb2d-6a1e2fca2d3f
TRIGGER:PT0S
ACTION:DISPLAY
DESCRIPTION:alarmDate1
END:VALARM
EOF

    xlog $self, "PROPPATCH default alerts on $davUrl";
    my $proppatchXml = <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<D:propertyupdate xmlns:D="DAV:" xmlns:C="urn:ietf:params:xml:ns:caldav">
  <D:set>
    <D:prop>
<C:default-alarm-vevent-datetime>$valarmDateTime</C:default-alarm-vevent-datetime>
    </D:prop>
  </D:set>
  <D:set>
    <D:prop>
<C:default-alarm-vevent-date>$valarmDate</C:default-alarm-vevent-date>
    </D:prop>
  </D:set>
</D:propertyupdate>
EOF
    $caldav->Request('PROPPATCH', $davUrl,
        $proppatchXml, 'Content-Type' => 'text/xml');

    xlog $self, "Get CalDAV default alarms for $objectType $objectId";
    $res = $jmap->CallMethods([
        ["$objectType/get", {
            ids => [$objectId],
            properties => [
                'caldavDefaultAlarmDateTime',
                'caldavDefaultAlarmDate',
            ],
        }, 'R1'],
    ], $using);
    $self->assert_not_null(
        $res->[0][1]{list}[0]{caldavDefaultAlarmDateTime}{guid});
    $self->assert_str_equals($valarmDateTime,
        $res->[0][1]{list}[0]{caldavDefaultAlarmDateTime}{content});
    $self->assert_equals(JSON::true,
        $res->[0][1]{list}[0]{caldavDefaultAlarmDateTime}{isDlist});

    $self->assert_not_null(
        $res->[0][1]{list}[0]{caldavDefaultAlarmDate}{guid});
    $self->assert_str_equals($valarmDate,
        $res->[0][1]{list}[0]{caldavDefaultAlarmDate}{content});
    $self->assert_equals(JSON::true,
        $res->[0][1]{list}[0]{caldavDefaultAlarmDate}{isDlist});

    xlog $self, "Can not update CalDAV default alarms for $objectType $objectId";

    $valarmDateTime = <<EOF;
BEGIN:VALARM
UID:c5f1c439-d18d-4c98-89e1-211ccf752dec
TRIGGER:-PT10M
ACTION:DISPLAY
DESCRIPTION:alarmTime2
END:VALARM
EOF

    $valarmDate = <<EOF;
BEGIN:VALARM
UID:bc0c8431-503e-427a-bb2d-6a1e2fca2d3f
TRIGGER:PT5S
ACTION:DISPLAY
DESCRIPTION:alarmDate2
END:VALARM
EOF
    $res = $jmap->CallMethods([
        ["$objectType/set", {
            update => {
                $objectId => {
                    caldavDefaultAlarmDateTime => $valarmDateTime,
                    caldavDefaultAlarmDate => $valarmDate,
                },
            },
        }, 'R1'],
    ], $using);
    $self->assert_str_equals('invalidProperties',
        $res->[0][1]{notUpdated}{$objectId}{type});

}

sub test_debug_defaultalerts_get_caldav
    :min_version_3_7 :needs_component_jmap
{
    my ($self) = @_;

    xlog $self, "Test CalDAV default alarms for CalendarPreferences";

    $self->t('CalendarPreferences', 'singleton',
        '/dav/calendars/user/cassandane');

    xlog $self, "Test CalDAV default alarms for Calendar";

    $self->t('Calendar', 'Default',
        '/dav/calendars/user/cassandane/Default');
}
